dsl Proteo

Concept:{1..1}:Named Artifact
	var string groupId
	var string version
	Concept:{0..1} License
		var string author
		var word:{GPL BSD} type
	Concept:{0..1} Imports
		Concept Dependency
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string name = '"Legio: " + self.identifier()' is private reactive
			var string groupId
			var string artifactId
			var string version
			var string effectiveVersion = "" is private
			var boolean transitive = true
			var string[] artifacts is private
			var boolean resolve = false is private
			var boolean toModule = false is private
			Concept Exclude
				var string groupId
				var string artifactId
			sub Compile
			sub Runtime
			sub Provided
			sub Test
	Concept:{0..1} WebImports
		var string webDirectory = "lib"
		Concept Resolution
			var string name
			var string version
		Concept:Named WebComponent
			var string url = ""
			var string version
		Concept:Named WebArtifact
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string groupId
			var string artifactId
			var string version
	Concept:{0..1} Box
		var string language
		var string version
		var string sdk
		var string targetPackage = "box"

	Concept:{0..1} Code
		var string targetPackage
	Concept Exports
		var Repository.Type repository
	Concept:{0..1} Package
		var word:{ModulesAndLibrariesExtracted LibrariesLinkedByManifest ModulesAndLibrariesLinkedByManifest} mode
		var boolean attachSources = false
		var boolean attachDoc = false
		var boolean includeTests = false
		var string classpathPrefix = empty
		var string finalName = empty
		has Parameter
		Concept MavenPlugin
			var string code

	Concept:CheckLevel:{0..1} Distribution
		var Repository.Release release
		var Repository.Language language = empty

		Concept:{0..1} onBitbucket
			var string owner
			var string slugName

	Concept:{0..1} QualityAnalytics
		var string url
		Concept:{0..1} Authentication
			var string token

	Concept:Named Deployment
		var string[] tags = empty
		var function:Destinations destinations = 'java.util.Arrays.asList(self.dev(), self.pro())' is private
		Concept:{0..1} Dev extends Destination
		Concept:{0..1} Pro extends Destination

Concept Destination is component
	var Server server
	var RunConfiguration runConfiguration
	var string url = empty
	var string project = empty
	Concept:{0..1} BugTracking
		var string[] slackUsers = empty
	Concept:{0..1} Requirements
		Concept Memory
		Concept CPU

Concept:CheckDuplicatedArguments RunConfiguration
	var string mainClass = ""
	has Argument

Concept:Named Server
	var string cesar

Concept Repository
	var string identifier
	Concept Type
		var string url
		var string mavenID = 'return self.core$().ownerAs(Repository.class).identifier();' is reactive final
		sub Release
		sub:{0..1} Snapshot
		sub:{0..1} Language

Facet Runnable on Artifact.Package
	var string mainClass

Facet MacOS on Artifact.Package with Runnable
	var string macIcon
	var string resourceDirectory = empty

Facet Windows on Artifact.Package with Runnable
	var string windowsIcon

Facet Level on Artifact
	Concept:{1..1} Model
		var string language
		var string version
		var string sdk
		var string effectiveVersion = "" is private
	sub Platform
	sub Product
	sub Solution

Concept Parameter is component
	var string name
	var string value
	var string description = ""

Concept Argument is component
	var string name
	var string value