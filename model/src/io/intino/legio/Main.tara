dsl Proteo

Concept:{1..1}:Named Project
	var string groupId
	var string version
	Concept:{0..1} License
		var word:{GPL BSD} type
	Concept:{0..1} Repositories
		Concept Repository
			var string url
			var string mavenId
			sub Release
			sub:{0..1} Snapshot
			sub:{0..1} Language

	Concept:{0..1} Dependencies
		Concept Dependency
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string name = '"Legio: " + self.identifier()' is private reactive
			var string groupId
			var string artifactId
			var string version
			var string effectiveVersion = "" is private
			var boolean transitive = true
			var string[] artifacts is private
			var boolean resolved = false is private
			var boolean toModule = false is private
			sub Compile
			sub Runtime
			sub Provided
			sub Test
	Concept:{0..1} WebDependencies
		var string webDirectory = "lib"
		Concept Resolution
			var string name
			var string version
		Concept:Named WebComponent
			var string url = ""
			var string version
		Concept:Named WebActivity
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string groupId
			var string artifactId
			var string version
	Concept:{0..1} Factory
		var string inPackage = ""
		Concept Language
			var string name
			var string version
			var string effectiveVersion = "" is private
		Concept:{0..1} Interface
			var string version

Concept:{0..1} LifeCycle
	Concept:{0..1} Package
		var word:{ModulesAndLibrariesExtracted LibrariesLinkedByManifest ModulesAndLibrariesLinkedByManifest} type
		var boolean attachSources = false
		var boolean attachDoc = false
		var boolean includeTests = false
		var string classpathPrefix = empty
		var string finalName = empty
		Concept MavenPlugin
			var string code

	Concept:{0..1} Distribution
		has:{0..1} Project.Repositories.Release
		has:{0..1} Project.Repositories.Snapshot
		has:{0..1} Project.Repositories.Language
		Concept:{0..1} Bitbucket
			var string user
			var string token

	Concept:{0..1} QualityAnalytics
		var string serverUrl
		Concept:{0..1} Authentication
			var string token

	Concept:{0..1} Deploy
		var string cesarURL
		var integer managementPort = 0
		Concept:{1..*} Destination
			var string specificServer = empty
			var string publicURL = empty
			Concept Requirements
				Concept Memory
				Concept CPU
			Concept:{0..1} Configuration
				has:Named Parameter
				Concept Service
					has:Named Parameter
				Concept:{0..1} Store
					var string path
			sub Pre
			sub Pro

Facet Runnable on LifeCycle.Package
	var string mainClass

Facet Level on Project.Factory
	sub Platform
	sub Application
	sub System

Concept Parameter is component
	var string  value = ""