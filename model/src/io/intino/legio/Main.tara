dsl Proteo

Concept:{1..1} Project
	var string groupId
	var string version
	Concept:{0..1} Repositories
		Concept Repository
			var string url
			var string mavenId
			sub Release
			sub Snapshot
			sub Distribution
			sub Language
	Concept:{0..1} Dependencies
		Concept Dependency
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string name = '"Legio: " + self.identifier()' is private reactive
			var string groupId
			var string artifactId
			var string version
			var boolean transitive = true
			sub Compile
			sub Runtime
			sub Test
	Concept:{0..1} Factory
		Concept:{0..1} Modeling
			var string language
			var string version
		var string generationPackage = ""
		var boolean persistent = false
		var integer refactorId = -1
	Concept:{0..1} Build
		var boolean attachSources = false
		var boolean attachDoc = false
		var boolean includeTests = false
		var string mainClass = empty
		var string finalName = empty
		Concept:{1..1} Package
			var word:{LibrariesExtracted OnlyLibrariesLinkedByManifest AllDependenciesLinkedByManifest} type
		Concept:{0..1} Plugin
			var string mavenCode
		Concept:{0..1} License
			var word:{GPL BSD} type
	Concept:{0..1} QualityAnalytics
		var string serverUrl
		Concept:{0..1} Authentication
			var string token
	Concept:{0..1} Delivery
		var string cesarURL

Facet Level on Project.Factory
	sub Platform
	sub Application
	sub System
