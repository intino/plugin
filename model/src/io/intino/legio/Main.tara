dsl Proteo

Concept:{1..1}:Named Project
	var string groupId
	var string version
	Concept:{0..1} License
		var word:{GPL BSD} type
	Concept:{0..1} Repositories
		Concept Repository
			var string url
			var string mavenId
			sub Release
			sub:{0..1} Snapshot
			sub:{0..1} Language
	Concept:{0..1} Dependencies
		Concept Dependency
			var string identifier = 'self.groupId() + ":" + self.artifactId() + ":" + self.version()' is private reactive
			var string name = '"Legio: " + self.identifier()' is private reactive
			var string groupId
			var string artifactId
			var string version
			var string effectiveVersion = "" is private
			var boolean transitive = true
			var string[] artifacts is private
			var boolean resolved = false is private
			var boolean toModule = false is private
			sub Compile
			sub Runtime
			sub Provided
			sub Test
	Concept:{0..1} WebDependencies
		var string webDirectory = "lib"
		Concept Resolution
			var string name
			var string version
		Concept:Named WebComponent
			var string url = ""
			var string version

	Concept:{0..1} Factory
		var string inPackage = ""
		Concept:{0..1} Interface
			var string version
		Concept:{0..1} Behavior
			var string version

Concept:{0..1} LifeCycle
	Concept:{0..1} Package
		var word:{ModulesAndLibrariesExtracted LibrariesLinkedByManifest ModulesAndLibrariesLinkedByManifest} type
		var boolean attachSources = false
		var boolean attachDoc = false
		var boolean includeTests = false
		var string classpathPrefix = empty
		var string finalName = empty
		Concept MavenPlugin
			var string code

	Concept:{0..1} Distribution
		has:{0..1} Project.Repositories.Release
		has:{0..1} Project.Repositories.Snapshot
		has:{0..1} Project.Repositories.Language
		Concept:{0..1} Bitbucket
			var string user
			var string token

	Concept:{0..1} QualityAnalytics
		var string serverUrl
		Concept:{0..1} Authentication
			var string token

	Concept:{0..1} Publishing
		var string cesarURL
		Concept:{1..*} Destiny
			var string publicURL
			Concept Requirements
				Concept Memory
				Concept CPU
			has:Named Parameter
			sub PreDeploy
			sub Deploy

Facet Runnable on LifeCycle.Package
	var string mainClass

Facet Level on Project.Factory
	var string language
	var string version
	var string effectiveVersion = "" is private
	sub Platform
	sub Application
	sub System

Concept Parameter is component
	var boolean required = false

Facet Type on Parameter
	var string type is reactive private
	sub Real
    	var double value
		var string type = "Double" is reactive private
	sub Integer
		var integer value
		var string type = "Integer" is reactive private
	sub Bool
    	var boolean value
		var string type = "Boolean" is reactive private
	sub Text
    	var string value
		var string type = "String" is reactive private